 ; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; The output file, setupCB.exe,  is placed in C:\Users\Ed Sowell\Documents\NetBeansProjects\EditJudgeAssignments\Output

;  2/6/2019 Modified to get CBDoc from C:\Users\jag_m\Documents\CBDev instead of D:\DocumentsD\JOCBusiness\Concours

#define MyAppName "ConcoursBuilder"
; Changed from 2.10 to 2.20 1/9/2017. Also reset Build number to 3 in C:\Users\Ed Sowell\Documents\NetBeansProjects\EditJudgeAssignments\src\version.properties
; Changed from 2.20 to 2.21 6/12/2017. Also reset Build number to 10 in C:\Users\Ed Sowell\Documents\NetBeansProjects\EditJudgeAssignments\src\version.properties
; Changed from 2.21 to 2.22 10/04/2017. Also reset Build number to 10 in C:\Users\Ed Sowell\Documents\NetBeansProjects\EditJudgeAssignments\src\version.properties
; Changed from 2.22 to 2.23 7/31/2018. Also reset Build number to 10 in C:\Users\Ed Sowell\Documents\NetBeansProjects\EditJudgeAssignments\src\version.properties
#define MyAppVersion "2.23"
#define MyAppPublisher "Edward F Sowell"
#define MyAppURL "http://www.efsowell.us/"
#define MyAppExeName "ConcoursBuilder.exe"
;#define MyAppIcon "leaperIcon48x48x32.ico"
#define MyAppIcon "CBIcon32x32.ico";

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{ABC9227A-1DEB-458C-BD16-2B7DFC211489}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
OutputDir=C:\Users\jag_m\Documents\CBDev\Output

OutputBaseFilename=setupCB
;OutputBaseFilename=setupCB_JOC
Compression=lzma
SolidCompression=yes
Password="Jags4ever!"
LicenseFile=C:\Users\jag_m\Documents\CBDev\CBDocs\licenseCB.txt

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

;Added 5/27/2017 to address installation issues with Windows 10
[Dirs]
Name: "{app}\CBDocs"; Permissions: users-modify

[Files]
; 32 & 64 bit JREs.  These were previously manually downloaded (NOT run)  from the Oracle site and saved in the  My Documents/Downloads/Java directory
Source: "D:\DocumentsD\Downloads\Java\jre-8u121-windows-i586.exe"; DestDir: "{tmp}"; DestName: "JREInstaller.exe"; \
    Flags: deleteafterinstall; AfterInstall: RunJavaInstaller(); \
    Check: (NOT IsWin64) AND InstallJava();
Source: "D:\DocumentsD\Downloads\Java\\jre-8u121-windows-x64.exe"; DestDir: "{tmp}"; DestName: "JREInstaller.exe"; \
    Flags: deleteafterinstall; AfterInstall: RunJavaInstaller(); \
    Check: IsWin64 AND InstallJava();
          
Source: "C:\Users\jag_m\Documents\CBDev\CBDocs\AboutCB.txt";         DestDir: "{app}\CBDocs"; Flags: ignoreversion
Source: "C:\Users\jag_m\Documents\CBDev\CBDocs\AcknowledgmentsCB.txt"; DestDir: "{app}\CBDocs"; Flags: ignoreversion
; NOTE:  ConcoursBuilder.exe is created with Launch4J and placed the dist folder
Source: "C:\Users\jag_m\Documents\NetBeansProjects\EditJudgeAssignments\dist\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
; All jar files in the NetBeansProjects\EditJudgeAssignments\dist\lib directory must be put in {app}\lib
;
Source: "C:\Users\jag_m\Documents\NetBeansProjects\EditJudgeAssignments\dist\lib\*"; DestDir:  "{app}\lib"
; NOTE:  SoCalBase071115.db included only for distribution to JOC, SDJC, & IEJC. The setup file is password protected.
; NOTE:  It's best not to include db files with personal data for real people in the distribution.
;
;Source: "C:\Users\jag_m\Documents\JOCBusiness\Concours\SoCalBase071115.db"; DestDir: "{userdocs}\My Concourses" ;   Flags: ignoreversion
Source: "C:\Users\jag_m\Documents\CBDev\Concourses\AnonBase.db"; DestDir: "{userdocs}\My Concourses" ;   Flags: ignoreversion
Source: "C:\Users\jag_m\Documents\CBDev\Concourses\EmptyBase.db"; DestDir: "{userdocs}\My Concourses" ;   Flags: ignoreversion
Source: "C:\Users\jag_m\Documents\CBDev\CBDocs\Score_Sheets v07022016Form.pdf"; DestDir: "{app}\CBDocs"
Source: "C:\Users\jag_m\Documents\CBDev\CBDocs\2005_windscreen_c_Form.pdf"; DestDir: "{app}\CBDocs"
Source: "C:\Users\jag_m\Documents\CBDev\CBDocs\2005_windscreen_d_Form.pdf"; DestDir: "{app}\CBDocs"
Source: "C:\Users\jag_m\Documents\CBDev\CBDocs\2005_windscreen_s_Form.pdf"; DestDir: "{app}\CBDocs"
Source: "C:\Users\jag_m\Documents\CBDev\CBDocs\2005_windscreen_disp_Form.pdf"; DestDir: "{app}\CBDocs"
Source: "C:\Users\jag_m\Documents\CBDev\CBDocs\CBIcon32x32.ico"; DestDir: "{app}\CBDocs"
Source: "C:\Users\jag_m\Documents\CBDev\CBDocs\ConcoursBuilder Users Manual.pdf"; DestDir: "{app}\CBDocs"
Source: "C:\Users\jag_m\Documents\CBDev\JCNAClassRules.csv"; DestDir: "{userdocs}\My Concourses";   Flags: ignoreversion
Source: "C:\Users\jag_m\Documents\CBDev\JCNAClasses.csv"; DestDir: "{userdocs}\My Concourses" ;   Flags: ignoreversion
; A token Existing concours, TutorialE5J5
;Source: "C:\Users\jag_m\Documents\JOCBusiness\Concours\TutorialE5J5\*"; DestDir: "{userdocs}\My Concourses" ;   Flags: ignoreversion
Source: "C:\Users\jag_m\Documents\CBDev\Concourses\TutorialE5J5\TutorialE5J5.db"; DestDir: "{userdocs}\My Concourses\TutorialE5J5\" ;   Flags: ignoreversion
         

[INI]
Filename: "{app}\CBDocs\ConcoursBuilderUser.ini"; Section: "InstallSettings"; Flags: uninsdeletesection
Filename: "{app}\CBDocs\ConcoursBuilderUser.ini"; Section: "InstallSettings"; Key: "InstallPath"; String: "{app}"
Filename: "{app}\CBDocs\ConcoursBuilderUser.ini"; Section: "InstallSettings"; Key: "ConcoursBuilderDataPath"; String: "{userdocs}\My Concourses"
Filename: "{app}\CBDocs\ConcoursBuilderUser.ini"; Section: "InstallSettings"; Key: "ConcoursBuilderDocsPath"; String: "{app}\CBDocs"
Filename: "{app}\CBDocs\ConcoursBuilderUser.ini"; Section: "InstallSettings"; Key: "Version"; String: {#MyAppVersion}


[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\CBDocs\{#MyAppIcon}"; Tasks: desktopicon


;[Run]
;Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: shellexec postinstall skipifsilent

; JRE installation
[code]
procedure RunJavaInstaller();
var
  StatusText: string;
  ResultCode: Integer;
  Path, Parameters: string;
begin
  Path := '{tmp}\JREInstaller.exe';
  { from http://docs.oracle.com/javase/8/docs/technotes/guides/install/config.html#table_config_file_options }
  Parameters := '/s INSTALL_SILENT=Enable REBOOT=Disable SPONSORS=Disable REMOVEOUTOFDATEJRES=1';
  StatusText:= WizardForm.StatusLabel.Caption;
  WizardForm.StatusLabel.Caption:='Installing the Java Runtime Environment (JRE). It might take a few minutes, so be patient :-)';
  WizardForm.ProgressGauge.Style := npbstMarquee;
  try
    if not Exec(ExpandConstant(Path), Parameters, '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
    begin
      { we inform the user we couldn't install the JRE }
      MsgBox('Java runtime environment install failed with error ' + IntToStr(ResultCode) + 
        '. Try installing it manually and try again to install MyProg.', mbError, MB_OK);
    end;
  finally
    WizardForm.StatusLabel.Caption := StatusText;
    WizardForm.ProgressGauge.Style := npbstNormal;
  end;
end;

procedure DecodeVersion(verstr: String; var verint: array of Integer);
var
  i,p: Integer; s: string;
begin
  { initialize array }
  verint := [0,0,0,0];
  i := 0;
  while ((Length(verstr) > 0) and (i < 4)) do
  begin
    p := pos ('.', verstr);
    if p > 0 then
    begin
      if p = 1 then s:= '0' else s:= Copy (verstr, 1, p - 1);
      verint[i] := StrToInt(s);
      i := i + 1;
      verstr := Copy (verstr, p+1, Length(verstr));
    end
    else
    begin
      verint[i] := StrToInt (verstr);
      verstr := '';
    end;
  end;
end;


function CompareVersion (ver1, ver2: String) : Integer;
var
  verint1, verint2: array of Integer;
  i: integer;
begin
  SetArrayLength (verint1, 4);
  DecodeVersion (ver1, verint1);

  SetArrayLength (verint2, 4);
  DecodeVersion (ver2, verint2);

  Result := 0; i := 0;
  while ((Result = 0) and ( i < 4 )) do
  begin
    if verint1[i] > verint2[i] then
      Result := 1
    else
      if verint1[i] < verint2[i] then
        Result := -1
      else
        Result := 0;
    i := i + 1;
  end;
end;


function InstallJava() : Boolean;
var
  //ErrCode: Integer;
  JVer: String;
  InstallJ: Boolean;
begin
  RegQueryStringValue(
    HKLM, 'SOFTWARE\JavaSoft\Java Runtime Environment', 'CurrentVersion', JVer);
  InstallJ := true;
  if Length( JVer ) > 0 then
  begin
    if CompareVersion(JVer, '1.8') >= 0 then
    begin
      InstallJ := false;
    end;
  end;
  Result := InstallJ;
end;